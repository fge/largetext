/*
 * Copyright (c) 2014, Francis Galiegue (fgaliegue@gmail.com)
 *
 * This software is dual-licensed under:
 *
 * - the Lesser General Public License (LGPL) version 3.0 or, at your option, any
 *   later version;
 * - the Apache Software License (ASL) version 2.0.
 *
 * The text of both licenses is available under the src/resources/ directory of
 * this project (under the names LGPL-3.0.txt and ASL-2.0.txt respectively).
 *
 * Direct link to the sources:
 *
 * - LGPL 3.0: https://www.gnu.org/licenses/lgpl-3.0.txt
 * - ASL 2.0: http://www.apache.org/licenses/LICENSE-2.0.txt
 */

/*
 * This has to be here... It will not be taken into account in common.gradle!
 *
 * Looks like a serious bug... See also:
 *
 * http://forums.gradle.org/gradle/topics/buildscript_in_subprojects_ignored
 */
buildscript {
    repositories {
        maven { url "https://repo.spring.io/plugins-release" };
        mavenCentral();
    }
    dependencies {
        classpath(group: "org.springframework.build.gradle",
            name: "propdeps-plugin", version: "0.0.7")
        classpath(group: "info.solidsoft.gradle.pitest",
            name: "gradle-pitest-plugin", version: "1.1.2");
    }
};


apply(plugin: "java");
apply(plugin: "maven");
apply(plugin: "signing");
apply(plugin: "idea");
apply(plugin: "eclipse");
apply(plugin: "propdeps");
apply(plugin: "propdeps-maven");
apply(plugin: "propdeps-idea");
apply(plugin: "propdeps-eclipse");
apply(plugin: "info.solidsoft.pitest");

group = "com.github.fge";
version = "0.2.1-SNAPSHOT";
description = "A library for handling large text files as CharSequences";
sourceCompatibility = "1.7";
targetCompatibility = "1.7"; // defaults to sourceCompatibility

/*
 * Repositories to use
 */
repositories {
    mavenCentral();
}

/*
 * List of dependencies
 */
dependencies {
    provided(group: "com.google.code.findbugs", name: "jsr305",
        version: "3.0.0");
    compile(group: "com.google.guava", name: "guava", version: "30.1-android");
    testCompile(group: "org.testng", name: "testng", version: "6.8.13") {
        exclude(group: "junit", module: "junit");
        exclude(group: "org.beanshell", module: "bsh");
        exclude(group: "org.yaml", module: "snakeyaml");
    };
    testCompile(group: "org.mockito", name: "mockito-core", version: "1.10.17");
    testCompile(group: "org.assertj", name: "assertj-core", version: "2.9.1");
    testCompile(group: "org.apache.commons", name: "commons-lang3", version: "3.11")
}

test {
    useTestNG() {
        useDefaultListeners = true;
    };
}

/*
 * Necessary to generate the source and javadoc jars
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources";
    from sourceSets.main.allSource;
}

/*
 * Javadoc: we need to tell where the overview.html is, it will not pick it up
 * automatically...
 */

javadoc {
    options.overview = "src/main/java/overview.html";
    // https://github.com/gradle/gradle/issues/11182
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_7) > 0) {
        options.addStringOption("-release", "7")
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc";
    from javadoc.destinationDir;
}

javadoc.options.links("https://docs.oracle.com/javase/7/docs/api/");
javadoc.options.links("https://static.javadoc.io/com.google.code.findbugs/jsr305/3.0.2/");
javadoc.options.links("https://static.javadoc.io/com.google.guava/guava/17.0/");

artifacts {
    archives jar;
    archives sourcesJar;
    archives javadocJar;
}

task pom {
    doLast {
        pom {}.writeTo("${projectDir}/pom.xml");
    }
}

/*
 * SIGNING
 */

project.ext {
    gitrwscm = sprintf("git@github.com:fge/%s", name);
    gitroscm = sprintf("https://github.com/fge/%s.git", name);
    projectURL = sprintf("https://github.com/fge/%s", name);
    sonatypeStaging = "https://oss.sonatype.org/service/local/staging/deploy/maven2/";
    sonatypeSnapshots = "https://oss.sonatype.org/content/repositories/snapshots/";
};

task checkSigningRequirements {
    doLast {
        def requiredProperties = [ "sonatypeUsername", "sonatypePassword" ];
        def noDice = false;
        requiredProperties.each {
            if (project.properties[it] == null) {
                noDice = true;
                System.err.printf("property \"%s\" is not defined!")
            }
        }
        if (noDice)
            throw new IllegalStateException("missing required properties for " +
                    "upload");
    }
}

uploadArchives {
    dependsOn(checkSigningRequirements);
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: "${sonatypeStaging}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }

            snapshotRepository(url: "${sonatypeSnapshots}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }
        }
    }
}

/*
 * Configure pom.xml on install, uploadArchives
 */
[
    install.repositories.mavenInstaller,
    uploadArchives.repositories.mavenDeployer
]*.pom*.whenConfigured { pom ->
    pom.project {
        name "${project.name}";
        description "${project.description}";
        packaging "jar";
        url "${projectURL}";

        scm {
            url "${gitrwscm}";
            connection "${gitrwscm}";
            developerConnection "${gitroscm}";
        }

        licenses {
            license {
                name "Lesser General Public License, version 3 or greater";
                url "http://www.gnu.org/licenses/lgpl.html";
                distribution "repo";
            };
            license {
                name "Apache Software License, version 2.0";
                url "http://www.apache.org/licenses/LICENSE-2.0";
                distribution "repo";
            }
        }

        developers {
            developer {
                id "fge";
                name "Francis Galiegue";
                email "fgaliegue@gmail.com";
            }
        }
    }
}

ext.forRelease = !version.endsWith("-SNAPSHOT");
signing {
    required { forRelease && gradle.taskGraph.hasTask("uploadArchives") };
    sign configurations.archives;
}

